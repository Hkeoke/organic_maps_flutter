group = "com.example.organic_maps_flutter"
version = "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "2.2.20"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.11.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    namespace = "app.organicmaps.sdk"

    compileSdk = 36
    
    ndkVersion = '28.2.13676358'

    buildFeatures {
        buildConfig = true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        main.java.srcDirs += "sdk/src/main/java"
        main.res.srcDirs += "sdk/src/main/res"
        main.assets.srcDirs += "sdk/src/main/assets"
        main.manifest.srcFile "sdk/src/main/AndroidManifest.xml"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 24
        
        externalNativeBuild {
            cmake {
                cppFlags '-fexceptions', '-frtti'
                cFlags '-fno-function-sections', '-fno-data-sections', '-Wno-extern-c-compat'
                arguments '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=c++_static',
                    '-DSKIP_TESTS=ON', '-DSKIP_TOOLS=ON'
                targets 'organicmaps'
            }
        }
        
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
        }
    }
    
    // We don't compress these extensions in assets/ because the FileReader can't read zip-compressed files from apk correctly.
    androidResources {
        noCompress = ['txt', 'bin', 'html', 'png', 'json', 'mwm', 'ttf', 'sdf', 'ui', 'config', 'csv', 'spv', 'obj', 'dat']
    }
    
    externalNativeBuild {
        cmake {
            version = '3.22.1+'
            path '../comaps/CMakeLists.txt'
        }
    }

    dependencies {
        coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
        
        // Dependencias del SDK
        implementation 'androidx.core:core:1.12.0'
        implementation 'androidx.annotation:annotation:1.7.1'
        implementation 'androidx.fragment:fragment:1.6.2'
        implementation 'androidx.lifecycle:lifecycle-process:2.7.0'
        implementation 'androidx.media:media:1.7.0'
        implementation 'androidx.recyclerview:recyclerview:1.3.2'
        implementation 'androidx.preference:preference:1.2.1'
        implementation 'com.google.android.material:material:1.11.0'
        implementation 'androidx.documentfile:documentfile:1.0.1'
        
        testImplementation("org.jetbrains.kotlin:kotlin-test")
        testImplementation("org.mockito:mockito-core:5.0.0")
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}
